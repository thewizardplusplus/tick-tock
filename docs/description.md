### ![](logo/logo.png) Общее описание

Tick-tock — интерпретируемый высокоуровневый язык программирования. Представляет собой конкурентную (по модели акторов) машину Тьюринга.

#### Исходники

Файлы исходного кода должны быть в кодировке UTF-8 и иметь концы строк как в UNIX (LF).

#### Ключевые слова

10 ключевых слов: `actor`, `class`, `state`, `message`, `let`, `start`, `send`, `set`, `return`, `when`.

#### Типы

Типизация: строгая динамическая.

Менеджмент памяти: сборка мусора.

##### Нулевой тип

Название: nil.

Тип: фиктивный тип; не хранит значение, а служит маркером его отсутствия; существует единственное значение данного типа.

Копирование: по значению.

Хранение: на стеке.

Определение: значение данного типа доступно через вызов функции рантайма `nil`.

##### Вещественные числа

Название: num.

Тип: с плавающей запятой, 8 байт.

Копирование: по значению.

Хранение: на стеке.

Определение:

- целые — `/\b((0x[\da-f]+)|(0[0-7]+)|(\d+(e\d+)?)|(\d+e[\+\-]\d+))\b/i`;
- вещественные — `/(\.\d+(e[\+\-]\d+)?)\b|\b\d+\.\d*((e[\+\-]\d+)?\b)?/i`;
- символы — `/'(\\x[\da-f]{2}|\\.|[^'\n])'/i`.

##### Списки

Название: list.

Тип: LISP-подобный список &mdash; или пустой список, или пара из головы и хвоста, где голова может иметь любой тип (и является хранимым элементом), а хвост является аналогичным списком.

Копирование: по ссылке.

Хранение:

- ссылки &mdash; на стеке;
- значения &mdash; в куче.

Определение:

- пустого списка &mdash; через оператор `[...]` или вызов функции рантайма `__empty_list__`;
- списка из головы и хвоста &mdash; через оператор `:` или вызов функции рантайма `__cons__`;
- списка из набора элементов &mdash; через оператор `[...]`.

Доступ к элементам:

- к количеству элементов &mdash; через вызов функции рантайма `size`;
- к голове списка &mdash; через вызов функции рантайма `head`;
- к хвосту списка &mdash; через вызов функции рантайма `tail`;
- к элементу по индексу &mdash; через оператор `...[...]` или вызов функции рантайма `__item__`.

##### Строки

Строками являются списки, хранящие коды символов. Используется кодировка UTF-32.

Определение:

- интерпретируемые:
  - в одинарных кавычках — `/'(\\x[\da-f]{2}|\\.|[^'\n])*?'/i`;
  - в двойных кавычках — `/"(\\x[\da-f]{2}|\\.|[^"\n])*?"/i`;
- сырые — `` /`[^`]*?`/ ``.

В интерпретируемых преобразуются управляющие последовательности.

Строки в одинарных кавычках допускаются, только если они длиннее 1 символа.

##### Хеш-таблицы

Название: hash.

Тип: ассоциативный массив, реализованный через хеш-таблицу; порядок ключей не гарантируется.

Копирование: по ссылке.

Хранение:

- ссылки &mdash; на стеке;
- значения &mdash; в куче.

Определение:

- пустой хеш-таблицы &mdash; через оператор `{...}` или вызов функции рантайма `__empty_hash__`;
- новой хеш-таблицы из старой и пары ключа и значения &mdash; через вызов функции рантайма `with`;
- новой хеш-таблицы из старой без определённого ключа &mdash; через вызов функции рантайма `with`;
- хеш-таблицы из набора пар ключей и значений &mdash; через оператор `{...}`.

Доступ:

- к количеству пар ключей и значений &mdash; через вызов функции рантайма `size`;
- к списку ключей &mdash; через вызов функции рантайма `keys`;
- к значению по ключу &mdash; через оператор `(...).identifier`, оператор `...[...]` или вызов функции рантайма `__item__`.

##### Классы акторов

Название: class.

Тип: фиктивный тип; не хранит значение, а служит образцом для создания акторов; каждое значение данного типа является отдельным подтипом и существует в единственном экземпляре.

Копирование: по ссылке.

Хранение:

- ссылки &mdash; на стеке;
- значения &mdash; в куче.

Определение:

```
"class", identifier, "(", [identifier, {",", identifier}, [","]], ")",
  state, {state},
";"
```

Здесь первый `identifier` — имя класса актора, а также имя переменной, в которой будет хранится значение, соответствующее определяемому подтипу. Остальные `identifier` — имена параметров класса актора.

Если число установленных аргументов больше числа объявленных параметров, лишние аргументы отбрасываются. Если число установленных аргументов меньше числа объявленных параметров, параметры, которым не хватает аргументов, получают значение `nil`.

Поддерживается висящая запятая на конце списка параметров.

##### Логический тип

В качестве значений логического типа используются значения других типов:

- ложным логическим значением являются:
  - значение `nil`;
  - число 0;
  - пустой список;
- истинным логическим значением являются:
  - числа, отличные от 0;
  - непустые списки;
  - любые классы акторов.

#### Сущности

##### Обработчики сообщений

Содержат имя обрабатываемого сообщения, список параметров обрабатываемого сообщения и список команд, которые необходимо выполнить при получении данного сообщения.

Объявление:

```
"message", identifier, "(", [identifier, {",", identifier}, [","]], ")",
  {command},
";"
```

Здесь первый `identifier` — имя обрабатываемого сообщения. Остальные `identifier` — имена параметров обрабатываемого сообщения.

Если число отправленных аргументов больше числа объявленных параметров, лишние аргументы отбрасываются. Если число отправленных аргументов меньше числа объявленных параметров, параметры, которым не хватает аргументов, получают значение `nil`.

Поддерживается висящая запятая на конце списка параметров.

##### Состояния акторов

Содержат имя состояния актора, список параметров состояния актора и список обработчиков сообщений, относящихся к этому состоянию.

Объявление:

```
"state", identifier, "(", [identifier, {",", identifier}, [","]], ")",
  {message},
";"
```

Здесь первый `identifier` — имя состояния актора. Остальные `identifier` — имена параметров состояния актора.

Если число установленных аргументов больше числа объявленных параметров, лишние аргументы отбрасываются. Если число установленных аргументов меньше числа объявленных параметров, параметры, которым не хватает аргументов, получают значение `nil`.

Поддерживается висящая запятая на конце списка параметров.

##### Акторы

Представляют собой зелёные (легковесные) потоки. Содержат список поддерживаемых состояний. По умолчанию находятся в состоянии `__initialization__`.

Объявление:

```
"actor", identifier, "(", [identifier, {",", identifier}, [","]], ")",
  state, {state},
";"
```

При объявлении актора неявно определяется соответсвующий ему класс.

Здесь первый `identifier` — имя актора, имя определяемого класса, а также имя переменной, в которой будет хранится значение, соответствующее определяемому классу. Остальные `identifier` — имена параметров актора и определяемого класса.

Если число установленных аргументов больше числа объявленных параметров, лишние аргументы отбрасываются. Если число установленных аргументов меньше числа объявленных параметров, параметры, которым не хватает аргументов, получают значение `nil`.

Поддерживается висящая запятая на конце списка параметров.

#### Команды

##### Команда `let`

Вычисляет выражение, результат вычисления устанавливает указанной переменной. Если такой переменной ещё нет, то она будет объявлена. Изменение значения переменной, равно как и её объявление, имеет эффект только ниже текущей команды. Операция синхронная.

Синтаксис:

```
"let", identifier, "=", expression
```

Здесь `identifier` — имя переменной.

Результатом команды является значение выражения.

##### Команда `start`

Создаёт актор по указанному классу (как образцу). Операция синхронная.

Синтаксис:

```
"start",
  (identifier | "[", expression, "]"),
  "(", [expression, {",", expression}, [","]], ")"
```

Здесь `identifier` — имя класса актора. `expression` — выражение, результат вычисления которого должен представлять собой значение, соответствующее классу акторов. Список `expression` — список выражений, результаты вычисления которых используются в качестве аргументов класса актора.

Поддерживается висящая запятая на конце списка выражений.

Результатом команды является значение `nil`.

##### Команда `send`

Отправляет всем акторам указанное сообщение с указанными аргументами. Операция асинхронная.

Синтаксис:

```
"send", identifier, "(", [expression, {",", expression}, [","]], ")"
```

Здесь `identifier` — имя сообщения. Список `expression` — список выражений, результаты вычисления которых используются в качестве аргументов сообщения.

Поддерживается висящая запятая на конце списка выражений.

Результатом команды является значение `nil`.

##### Команда `set`

Устанавливает текущему актору указанное состояние с указанными аргументами. Операция синхронная.

Синтаксис:

```
"set", identifier, "(", [expression, {",", expression}, [","]], ")"
```

Здесь `identifier` — имя состояния. Список `expression` — список выражений, результаты вычисления которых используются в качестве аргументов состояния.

Поддерживается висящая запятая на конце списка выражений.

Результатом команды является значение `nil`.

##### Команда `return`

Прерывает выполнение команд текущего обработчика сообщений.

Синтаксис:

```
"return"
```

Результатом команды является значение `nil`.

##### Выражение

Выражение может выступать в роли команды. В этом случае оно просто вычисляется, а результат вычисления отбрасывается. Может использоваться ради побочных эффектов вычисления. Операция синхронная.

Результатом команды является значение выражения.

#### Операции

Перечислены в порядке убывания приоритета.

| Приоритет | Операция           | Описание                                                | Ассоциативность | Типы                       | Функция           |
| --------- | ------------------ | ------------------------------------------------------- | --------------- | -------------------------- | ----------------- |
| 0         | `(...)`            | вложенное выражение                                     | —               | —                          | —                 |
| 0         | `[...]`            | определение списка из набора элементов                  | —               | —                          | —                 |
| 0         | `{...}`            | определение хеш-таблицы из набора пар ключей и значений | —               | —                          | —                 |
| 0         | `...(...)`         | вызов функции                                           | —               | —                          | —                 |
| 0         | `when...;`         | условное выражение                                      | —               | —                          | —                 |
| 1         | `(...).identifier` | доступ к элементам хеш-таблицы                          | левая           | хеш-таблица + любой        | `__item__`        |
| 1         | `...[...]`         | доступ к элементам списка/хеш-таблицы                   | левая           | список/хеш-таблица + любой | `__item__`        |
| 2         | `-`                | унарный минус                                           | правая          | число                      | `__neg__`         |
| 2         | `~`                | побитовое отрицание                                     | правая          | число                      | `__bitwise_not__` |
| 2         | `!`                | логическое отрицание                                    | правая          | любой                      | `__logical_not__` |
| 3         | `*`                | умножение                                               | левая           | числа                      | `__mul__`         |
| 3         | `/`                | деление                                                 | левая           | числа                      | `__div__`         |
| 3         | `%`                | остаток от деления                                      | левая           | числа                      | `__mod__`         |
| 4         | `+`                | сложение/конкатенация                                   | левая           | числа/списки               | `__add__`         |
| 4         | `-`                | вычитание                                               | левая           | числа                      | `__sub__`         |
| 5         | `<<`               | сдвиг влево                                             | левая           | числа                      | `__lshift__`      |
| 5         | `>>`               | сдвиг вправо                                            | левая           | числа                      | `__rshift__`      |
| 5         | `>>>`              | беззнаковый сдвиг вправо                                | левая           | числа                      | `__urshift__`     |
| 6         | `&`                | побитовая конъюнкция                                    | левая           | числа                      | `__and__`         |
| 7         | `^`                | побитовая исключающая дизъюнкция                        | левая           | числа                      | `__xor__`         |
| 8         | `\|`               | побитовая дизъюнкция                                    | левая           | числа                      | `__or__`          |
| 9         | `<`                | меньше                                                  | левая           | nil/числа/списки           | `__lt__`          |
| 9         | `<=`               | меньше или равно                                        | левая           | nil/числа/списки           | `__le__`          |
| 9         | `>`                | больше                                                  | левая           | nil/числа/списки           | `__gt__`          |
| 9         | `>=`               | больше или равно                                        | левая           | nil/числа/списки           | `__ge__`          |
| 10        | `==`               | равно                                                   | левая           | любые                      | `__eq__`          |
| 10        | `!=`               | не равно                                                | левая           | любые                      | `__ne__`          |
| 11        | `&&`               | конъюнкция                                              | левая           | любые                      | —                 |
| 12        | `\|\|`             | дизъюнкция                                              | левая           | любые                      | —                 |
| 13        | `??`               | нулевое слияние                                         | левая           | любые                      | —                 |
| 14        | `:`                | определение списка из головы и хвоста                   | правая          | любой + список             | `__cons__`        |

##### Вложенное выражение

Синтаксис:

```
"(", expression, ")"
```

##### Определение списка из набора элементов

Синтаксис:

```
"[", [expression, {",", expression}, [","]], "]"
```

Здесь список `expression` — список выражений, результаты вычисления которых используются в качестве элементов списка.

Поддерживается висящая запятая на конце списка выражений.

##### Определение хеш-таблицы из набора пар ключей и значений

Синтаксис:

```
"{", [hash table entry, {",", hash table entry}, [","]], "}"
```

Здесь `hash table entry` — пара ключа и значения.

Синтаксис пары ключа и значения:

```
(identifier | "[", expression, "]"), ":", expression
```

Здесь `identifier` — имя ключа (будет использоваться как строковый ключ). Первое `expression` — выражение, результат вычисления которого будет использоваться как ключ. Второе `expression` — выражение, результат вычисления которого будет использоваться как значение.

Поддерживается висящая запятая на конце списка пар ключей и значений.

##### Вызов функции

Синтаксис:

```
identifier, "(", [expression, {",", expression}, [","]], ")"
```

Здесь `identifier` — имя функции. Список `expression` — список выражений, результаты вычисления которых используются в качестве аргументов функции.

Поддерживается висящая запятая на конце списка выражений.

##### Условное выражение

Синтаксис:

```
"when",
  {conditional case},
";"
```

Здесь `conditional case` — ветка условия.

Синтаксис ветки условия:

```
"=>", expression,
  {command}
```

При вычислении условного выражения ветки условия вычисляются последовательно одна за другой до тех пор, пока выражение в ветке не будет истинным. Если такая ветка будет найдена, вычисление веток останавливается, и начинают выполняться команды в найденной ветке. Результат последней выполненной команды будет возвращён как результат условного выражения.

##### Доступ к элементам списка/хеш-таблицы

Синтаксис:

```
expression, (".", identifier | "[", expression, "]")
```

Здесь первое `expression` — выражение, результат вычисления которого должен представлять собой либо список, либо хеш-таблицу. `identifier` — имя ключа (будет использоваться как строковый ключ). Второе `expression` — выражение, результат вычисления которого будет использоваться как ключ.

##### Битовые операции

Перед применением битовой операции её операнды преобразуются из числа с плавающей запятой в целое размером 8 байт. Результат битовой операции преобразуется обратно в число с плавающей запятой размером 8 байт.

В большинстве случаев операнды преобразуются в знаковое целое число. Исключением являются операции сдвига — их второй операнд преобразуется в беззнаковое целое указанного выше размера.

Так же особым случаем является беззнаковый сдвиг вправо. Если его первый аргумент отрицательный, к нему прибавляется число `1 << 32`. Так что для верности вычислений в этом случае размер операнда не должен превышать 4 байт.

##### Конъюнкция, дизъюнкция и нулевое слияние

Конъюнкция, дизъюнкция и нулевое слияние являются ленивыми операциями — они вычисляют правый операнд только при необходимости. При этом результатом данных операций является последний вычисленный операнд.

Условия вычисления правого операнда:

| Операция        | Условие                                                   |
| --------------- | --------------------------------------------------------- |
| конъюнкция      | левый операнд соответсвует истинному логическому значению |
| дизъюнкция      | левый операнд соответсвует ложному логическому значению   |
| нулевое слияние | левый операнд равен значению `nil`                        |

#### Точка входа

Интерпретатор асинхронно запускает все предоставленные акторы. После чего отправляет всем сообщение `__initialize__`.

#### Комментарии

Определение:

- однострочные — `/\/\/.*/`;
- многострочные — `/\/\*.*?\*\//s`.
