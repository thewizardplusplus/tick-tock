// https://www.rosettacode.org/wiki/Guess_the_number
actor
  state __initialization__
    message __initialize__()
      let secret = floor(random() * 10) + 1
      send guess_number(secret)
    ;

    message guess_number(secret)
      out("Input a number from 1 to 10 inclusive: ")

      let line = inln(-1)
      when
        => line == nil
          errln("Error: unable to read the number.")
          send guess_number(secret)
          return
      ;

      let number = num(line)
      when
        => number == nil
          errln("Error: unable to parse the number.")
          send guess_number(secret)
          return
        => number < 1 || number > 10
          errln("Error: number out of the range.")
          send guess_number(secret)
          return
        => number != secret
          outln("Failure.")
          send guess_number(secret)
          return
      ;

      outln("Success.")
    ;
  ;
;
