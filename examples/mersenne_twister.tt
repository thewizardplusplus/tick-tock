actor InitialStateGenerator()
  state __initialization__(state_)
    message generate_initial_state(seed_, previous_item, item_index)
      when
        => state_ == nil
          let first_item = seed_ & 0xffffffff
          set __initialization__([first_item])

          send generate_initial_state(nil, first_item, 1)

          return
        => item_index == 624
          send initial_state_generated(state_)
          return
      ;

      let item = 1812433253 * (previous_item ^ previous_item >> 30) + item_index & 0xffffffff
      set __initialization__(state_ + [item])

      send generate_initial_state(nil, item, item_index + 1)
    ;
  ;
;

actor StateGenerator()
  state __initialization__(state_)
    message generate_state(state_next, item_index)
      let state_next = state_next || []
      let item_index = item_index || 0
      when
        => state_ == nil
          set initial_state_generation_waiting()

          let seed_ = time() * 1e9
          send generate_initial_state(seed_)

          return
        => item_index == 624
          set __initialization__(state_next)
          send state_generated(state_next)

          return
      ;

      let y = (state_[item_index] & 0x80000000) + (state_[(item_index + 1) % 624] & 0x7fffffff)
      let item = state_[(item_index + 397) % 624] ^ y >> 1 ^ [0, 0x9908b0df][y & 1]
      send generate_state(state_next + [item], item_index + 1)
    ;
  ;

  state initial_state_generation_waiting()
    message initial_state_generated(state_)
      set __initialization__(state_)
      send generate_state()
    ;
  ;
;

actor NumberGenerator()
  state __initialization__(state_)
    message generate_number()
      let state_ = state_ || []
      when
        => state_ == []
          set state_generation_waiting()
          send generate_state()

          return
      ;

      let number = head(state_)
      let number = number ^ number >> 11
      let number = number ^ number << 7 & 0x9d2c5680
      let number = number ^ number << 15 & 0xefc60000
      let number = number ^ number >> 18
      send number_generated(number)

      set __initialization__(tail(state_))
    ;
  ;

  state state_generation_waiting()
    message state_generated(state_)
      set __initialization__(state_)
      send generate_number()
    ;
  ;
;

actor Main()
  state __initialization__()
    message __initialize__()
      send generate_number()
    ;

    message state_generated(state_)
      outln(str(state_))
    ;

    message number_generated(number)
      outln(str(number))
      sleep(0.01)

      send __initialize__()
    ;
  ;
;
