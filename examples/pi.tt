// https://en.wikipedia.org/wiki/Gauss-Legendre_algorithm
actor Main()
  state __initialization__()
    message __initialize__()
      send evaluate_pi(1e-6)
    ;

    message evaluate_pi(epsilon, a, b, t, p)
      let a = a || 1
      let b = b || 1 / sqrt(2)
      let t = t || 1 / 4
      let p = p || 1

      let a_next = (a + b) / 2
      let b_next = sqrt(a * b)
      let t_next = t - p * pow(a - a_next, 2)
      let p_next = 2 * p
      when
        => abs(a - b) > epsilon
          send evaluate_pi(epsilon, a_next, b_next, t_next, p_next)
          return
      ;

      let pi = pow(a_next + b_next, 2) / (4 * t_next)
      send pi_evaluated(pi)
    ;

    message pi_evaluated(pi)
      outln("Pi = " + str(pi) + ".")
    ;
  ;
;
